title Sheepfold
author Nuno Das Neves
homepage www.puzzlescript.net
text_color #ffaabb
background_color darkgreen

run_rules_on_level_start
realtime_interval 0.5
again_interval 0.1
(key_repeat_interval 0.2)
throttle_movement

(zoomscreen 8x8)
flickscreen 16x16

debug

========
OBJECTS
========

grass .
#60b020 #509020
11111
0011
11100
11111
11001

cave_floor ,
darkbrown #664411
00000
00110
11000
00000
00011

cave_wall
#191714

cave_wall_f
#302519 #191714
11111
11111
11000
01010
00010

dark_0
transparent

dark_1
transparent

dark_2
black
.....
.0.0.
.....
.0...
...0.

dark_3
black
.00.0
...0.
.00..
...00
.0...

dark_4
black
000.0
0.000
0000.
00.00
0.000

dark_x
black

player p
black brown #eee blue
.000.
.010.
13331
13331
.333.

slash_x
white

sheep_r &
#ddd #aaa #fed060
...00
00012
0000.
2112.
2..2.

sheep_l
#ddd #aaa #fed060
...00
00012
0000.
2112.
2..2.

sheep_d
#ddd #aaa #fed060
...00
00012
0000.
2112.
2..2.

sheep_u
#ddd #aaa #fed060
...00
00012
0000.
2112.
2..2.

dead_sheep
#ddd #aaa #fed060 red
.....
2..2.
2113.
00302
03301

gloves
darkgreen #aa2299
.....
.....
1...1
0...0
.....

gloves_item
darkgreen #aa2299
...00
..000
00.11
000..
11...

axe
black orange
.....
.....
.....
0111.
00...

axe_item
orange black
.0.1.
.1111
.0111
.0.1.
.0...

candle_item
#ff6600 #eed0a0 white #ffee00 gray
..0..
.030.
.020.
..4..
..4..

candle_unlit
#ff6600 #eed0a0 white yellow
.....
.....
...1.
...1.
.....

candle_lit
#ff6600 #eed0a0 white yellow
.....
...0.
..323
...1.
.....

stone_wall #
darkgray darkbrown gray
22212
10110
01001
10010
00010

rock *
gray darkgray
.000.
00000
00000
10001
11111

tree ;
darkbrown #306020 #204010
..1..
.111.
21221
22121
.222.

tree_stump
darkbrown
.....
.....
.....
..0..
.....

diamond $
orange white brown
.110.
11000
00002
.002.
..2..

=======
LEGEND
=======

background = grass
wall = stone_wall or cave_wall or cave_wall_f
ground_item = gloves_item or axe_item or diamond or candle_item
held_item = gloves or axe or candle_unlit or candle_lit
sheep = sheep_r or sheep_l or sheep_d or sheep_u
pushable = dead_sheep
obstacle = player or wall or rock or tree or sheep or pushable
light = candle_item or candle_lit
] = cave_floor and cave_wall
dark = dark_0 or dark_1 or dark_2 or dark_3 or dark_4 or dark_x
g = gloves_item
a = axe_item
c = cave_floor and candle_item

=======
SOUNDS
=======

rock move 36772507
(player action 75964507)

(get gloves)
sfx0 12173103

(tree cut)
sfx1 75964507

(sheep die)
sfx2 98795903

(candle on/off)
sfx3 8177908

sfx4 87047508

(spooooky echo 24371308)

(spooky shudder 92370508)

(aah.. 24200708)

(baaaar 96148508)
(slow whooosh 61744508)
(shhheeew 83395308)
(badup 17639903)

================
COLLISIONLAYERS
================

background
cave_floor
tree_stump
ground_item
obstacle
slash_x
gloves
axe
candle_unlit
candle_lit
dark

======
RULES
======

[ cave_floor ] -> [ cave_floor dark_x ]
down [ cave_wall | no wall ] -> [ cave_wall_f | ]

[ slash_x sheep ] -> [ dead_sheep ]
[ slash_x tree ] -> [ tree_stump ]

[ > player gloves | rock ] -> [ > player gloves | > rock ]
[ > player | pushable ] -> [ > player | > pushable ]
[ > player | pushable | obstacle no rock ] -> [ pushable | player | obstacle ]

[ > player axe | tree ] -> [ player axe | tree slash_x ] sfx1 again
[ > player axe | sheep ] -> [ player axe | sheep slash_x ] sfx2 again

[ action player candle_lit ] -> [ player candle_unlit ] sfx4
[ action player candle_unlit ] -> [ player candle_lit ] sfx3

(sheep)
[ stationary sheep_r no slash_x ] -> [ right sheep_r ]
[ stationary sheep_l no slash_x ] -> [ left sheep_l ]
[ stationary sheep_u no slash_x ] -> [ up sheep_u ]
[ stationary sheep_d no slash_x ] -> [ down sheep_d ]
right [ > sheep no slash_x | obstacle ] -> [ down sheep_d | obstacle ]
left [ > sheep no slash_x | obstacle ] -> [ up sheep_u | obstacle ]
up [ > sheep no slash_x | obstacle ] -> [ right sheep_r | obstacle ]
down [ > sheep no slash_x | obstacle ] -> [ left sheep_l | obstacle ]

(item pickup)
late [ player gloves_item ] -> [ player gloves ] sfx0
late [ player axe_item ] -> [ player axe ] sfx0
late [ player candle_item ] -> [ player candle_lit ] sfx0

(hold onto held items)
late [ player | ... | held_item ] -> [ player held_item | ... | ]

(let there be...candles!)
late [ light dark ] -> [ light dark_0 ]
late [ dark_0 | dark_x ] -> [ dark_0 | dark_1 ]
late [ dark_1 | dark_x ] -> [ dark_1 | dark_2 ]
late [ dark_2 | dark_x ] -> [ dark_2 | dark_3 ]
late [ dark_3 | dark_x ] -> [ dark_3 | dark_4 ]

==============
WINCONDITIONS
==============



=======
LEVELS
=======

]]]]]]]]]]]]]]]]################################################
]]]]]]]]]]]]]]]]#......#.......##.......#..##..##..............#
]]]]]],]]],,]]]]#.####.#.......##..............##..............#
]]],,,,,,,,,,,]]#.....$##......##..............##..............#
]]],,,,,,,,,,,,]#.....p.#......##..............##..............#
]],,,,,,,,,,,],]#...*..##......##..............##..............#
]],,,,,,,,,,,],,........#.#.&..##......#.......##........#.....#
]],,,,,],,,,,,,]#.#.a.g........................................#
]]],,,,,,,],,,]]#...............#...............#..............#
]]],],c,,,],,,]]#.....#......&.##..............##....&...#.....#
]],,],,,,,,,,,]]#..;;;;##;;....##...&..........##..............#
]]],],,,,,,,,]]]#......#.......##..............##..............#
]]]],,],,,,,,]]]#.......#......##..............##..............#
]]]]],]]],]]]]]]#.......#......##..............##..............#
]]]]]]]]]]]]]]]]#..............##..............##..............#
]]]]]]]]]]]]]]]]######.##################.##############.#######
######################.##################.##############.#######
#..............##..............##..............##..............#
#..............##..............##..............##..............#
#..............##..............##..............##..............#
#.......#......##.....................###......##......#.......#
#.......#......##.....###...............#......##......#.......#
#.......#......##........#.....##......##......##.....#.#......#
#.......#......##.......##.....##.......##............#.#......#
#.......#..............##......##.......##.....##......###.....#
#.......#......##.....##.......##..&...##......##.......#......#
#..............##.....####.....##.....##.......##.....&.#......#
#..............##..............##..............##..............#
#..............##..............##..............##..............#
#..............##..............##..............##..............#
#..............##..............##..##..........##..............#
########.#############.#################.###############.#######
########.#############.##########.######.###############.#######
#..............##..............##..............##..............#
#..............##..............##..............##..............#
#..............##..............##..............##..............#
#.....###......##..............##..............##..............#
#....#.........##..............##..............##..............#
#....##........##.......#......##......###.....##.......##.....#
#.....###......##......##......##..&.....#.....##......###.....#
#.....#.#......##......#.......##.......##.....##......#.#.....#
#...####.......##......###.....##.......#......##....&.###.....#
#..............##......#.#.....##..............##.....&#.#.....#
#........&.....##.......##..&..##..............##.......##.....#
#..............##..............##..............##..............#
#..............##..............##..............##..............#
#..............##..............##..............##..............#
#######.################.###############.###############.#######
#######.################.###############.###############.#######
#..............##..............##..............##..............#
#..............##..............##..............##..............#
#..............##......&.......##..............##..............#
#.....###......##..............##........&.....##..&...........#
#....##..#...&.##.....##.......##..............##..............#
#....#...#.....##.....#........##....##.#......##..............#
#.....####.....##........##....##.....#.##.....##.....#.##.....#
#........#.............#.#.#..........#..#.....##.....#..#.....#
#........#.....##......#.###...##.....#..#............#.##.....#
#........#.....##......#.##....##.....#........##.....#.###....#
#..............##..............##..............##..............#
#..............##..............##..............##..............#
#..............##..............##..............##..............#
#..............##..............##..............##..............#
################################################################

