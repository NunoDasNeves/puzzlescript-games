title Sheepfold
author Nuno Das Neves
homepage www.puzzlescript.net
text_color #ffaabb
background_color darkgreen

(this messes up editing)
(run_rules_on_level_start)
realtime_interval 0.5
again_interval 0.1
(key_repeat_interval 0.2)
throttle_movement

(zoomscreen 8x8)
flickscreen 16x16

debug

========
OBJECTS
========

grass
#60b020 #509820
11111
00111
11100
11111
11001

dirt_edge
#765422 #886630 #872 #509820
02232
30110
12330
20023
32011

tree_ground
#582 #472
11110
00111
11100
11111
11001


tree_edge
#582 #472
0.01.
1...0
..11.
110.1
0..00

grass_fold
#60b020 #509820
01111
00110
11100
10111
11001

cave_floor
#373025 #453721
00000
00110
11000
00000
00011

cave_wall
#191714

cave_wall_f
#302519 #191714
11111
11111
11000
01010
00010

dirt
#765422 #886630
00000
00110
11000
00000
00011

rock_wall_u
#352721 darkbrown
.....
.....
.....
.000.
00000

rock_wall_l
#352721 darkbrown
.....
....0
....0
....0
.....

rock_wall_r
#352721 darkbrown
.....
0....
0....
0....
.....

rock_wall
#352721 darkbrown
00100
10000
00000
01000
00001

rock_wall_f
darkbrown #352721
11111
01101
01010
01010
101.1

rock_ledge
darkbrown #765411 brown #664411 #352721
23332
02221
44040
44044
34401

cave_ledge
darkbrown #302519 #664411 #352721  #191714  
23332
02221
44040
44044
34401

dark_0
transparent

dark_1
transparent

dark_2
black
.....
.0.0.
.....
.0...
...0.

dark_3
black
.00.0
...0.
.00..
...00
.0...

dark_4
black
000.0
0.000
0000.
00.00
0.000

dark_x
black

player
black brown #eee #35f
.000.
.010.
13331
13331
.333.

slash_x
white

sheep_r
#ddd #aaa #fed060 #deb040
...00
00012
0000.
2112.
2..2.

sheep_l
#ddd #aaa #fed060 #deb040
00...
21000
.0000
.2112
.2..2

sheep_d
#ddd #aaa #fed060 #deb040
.000.
00200
10301
.111.
.2.2.

sheep_u
#ddd #aaa #fed060
.000.
00000
01010
.111.
.2.2.

sheep_f_r
#ddd #aaa #fed060 #deb040 #19f
...00
00412
0004.
2112.
2..2.

sheep_f_l
#ddd #aaa #fed060 #deb040 #19f
00...
21400
.4000
.2112
.2..2

sheep_f_d
#ddd #aaa #fed060 #deb040 #19f
.000.
04240
11411
.111.
.2.2.

sheep_f_u
#ddd #aaa #fed060 #deb040 #19f
.404.
00000
01010
.111.
.2.2.

sheep_f_np_0
transparent

sheep_f_np_1
transparent

sheep_f_np_2
transparent

sheep_f_np
transparent

sheep_in_fold
#ddd #aaa #fed060
.....
...00
00012
0000.
22022
(
dead_sheep
#ddd #aaa #fed060 red
.....
2..2.
2113.
00302
03301
)

gloves
darkgreen #aa2299 #223333 #ffbb00
.....
.....
.....
.232.
.....

gloves_item
darkgreen #aa2299 #223333 brown #ffbb00
.....
.222.
2...2
00400
.....

axe
orange
.....
....0
....0
.....
....0

axe_item
orange black
.0.1.
.1111
.0111
.0.1.
.0...

candle_item
#ff6600 #eed0a0 white #ffee00 gray
..0..
.030.
.020.
..4..
..4..

candle_unlit
#ff6600 #eed0a0 white yellow
.....
.....
.....
....1
.....

candle_lit
#ff6600 gray white yellow
.....
....0
....3
....1
.....

hair_item
orange #ffbb00 #253 black
.3...
.332.
..0..
.110.
.000.

hair_held
#132 #253 darkbrown #27f
01001
10.00
.....
.....
.....

hair_l
#132 #253 darkbrown
....2
.....
.....
.....
.....

hair_r
black
.....
.....
.....
.....
.....

hair_u
#132 #253 darkbrown
.....
.....
....2
.102.
10100

stone_wall 
darkgray darkbrown gray
22212
10110
01001
10010
00010

gate
brown darkbrown
00000
01..0
0.1.0
0..10
11111

rock
gray darkgray
.000.
00000
00000
10001
11111

tree_u
darkbrown #253 #132
.....
.....
.11..
1111.
.1122

tree
darkbrown #253 #132
1120.
02121
10.02
..001
.00..

water_cover_deep
#6bf #9ef #dff
.....
.....
.....
21222
00101

water_cover_shallow
#6bf #9ef #dff
.....
.....
.....
.....
21212

water_shallows
#6bf #9ef  #a9b #eff
3...2
22..3
...22
.33..
.22..

water_d_0
#6bf #9ef #dff
01110
00000
10001
01110
10001

water_d_1
#6bf #9ef #dff
10001
01110
00000
10001
01110

water_u_0
#6bf #9ef #dff
00000
01110
10001
01110
10001

water_u_1
#6bf #9ef #dff
01110
10001
01110
10001
00000

water_r_0
#6bf #9ef #dff
10010
01001
01001
01001
10010

water_r_1
#6bf #9ef #dff
01001
10100
10100
10100
01001

water_l_0
#6bf #9ef #dff
10100
01001
01001
01001
10100

water_l_1
#6bf #9ef #dff
01001
10010
10010
10010
01001

water_edge_r
#6bf #8df #6bf #323
3....
23...
23...
.....
.....

water_edge_l
#6bf #8df #6bf #323
.....
....3
...32
...32
.....

water_edge_u
#6bf #8df #6bf #323
.....
.....
.....
..33.
33223

water_edge_d
#6bf #8df #6bf #323
.22..
.....
.....
.....
.....

tree_stump_0
brown
.....
.....
.....
.....
.....

tree_stump_1
darkbrown
.....
.....
.....
..0..
.....

tree_stump_2
darkbrown
.....
.....
.....
..0..
.....

tree_stump_3
brown
.....
.....
.....
..0..
.....

tree_stump_4
brown
.....
.....
.....
..0..
.....

tree_stump_5
brown #408030
.....
.....
.0.1.
..0..
.....

tree_wall_u
darkbrown #242 #122
.....
.....
.....
.112.
11222

tree_wall_l
darkbrown #242 #122
.....
....1
....1
....1
.....

tree_wall_r
darkbrown #242 #122
.....
2....
2....
2....
.....

tree_wall
darkbrown #242 #122
21112
12121
22212
22122
.2222

tree_wall_f
darkbrown #242 #122
21112
12121
20212
20120
.2202

diamond
orange white brown
.110.
11000
00002
.002.
..2..

call_r
lightblue
..0..
...0.
....0
...0.
..0..

call_l
lightblue
..0..
.0...
0....
.0...
..0..

call_u
lightblue
..0..
.0.0.
0...0
.....
.....

call_d
lightblue
.....
.....
0...0
.0.0.
..0..

call_particle_0
lightblue orange
.....
.....
.....
0...0
.0.0.

call_particle_1
lightblue
.....
.....
00.00
..0..
.....

call_particle_2
lightblue
0...0
..0..
.....
.....
.....

anim_done
transparent

=======
LEGEND
=======

background = grass
rock_wall_any = rock_wall or rock_wall_f
tree_wall_any = tree_wall or tree_wall_f
wall = stone_wall or cave_wall or cave_wall_f or tree_wall_any or gate or rock_wall_any
ledge = cave_ledge or rock_ledge
ground_item = gloves_item or axe_item or diamond or candle_item or hair_item
hair = hair_held or hair_l or hair_r or hair_u
held_item = gloves or axe or candle_unlit or candle_lit or hair_held
sheep_wander = sheep_r or sheep_l or sheep_d or sheep_u
sheep_follow = sheep_f_r or sheep_f_l or sheep_f_d or sheep_f_u
sheep = sheep_wander or sheep_follow
sheep_f_np_any = sheep_f_np_0 or sheep_f_np_1 or sheep_f_np_2 or sheep_f_np
creature = sheep or player
pushable = rock
obstacle = player or wall or rock or tree or sheep or pushable or sheep_in_fold
call_blocker = wall or rock or tree
light = candle_item or candle_lit
] = cave_floor and cave_wall
dark = dark_0 or dark_1 or dark_2 or dark_3 or dark_4 or dark_x
tree_stump = tree_stump_0 or tree_stump_1 or tree_stump_2 or tree_stump_3 or tree_stump_4 or tree_stump_5
g = gloves_item
a = axe_item
c = cave_floor and candle_item
0 = sheep_r and cave_floor
1 = sheep_r and dirt
2 = rock and dirt
3 = cave_ledge and cave_floor
4 = hair_item
call = call_r or call_l or call_u or call_d
particle = call_particle_0 or call_particle_1 or call_particle_2
& = sheep_r
p = player
| = rock_wall and dirt
~ = water_d_0
! = water_u_0
@ = water_r_0
% = water_l_0
^ = water_shallows and water_d_0
water_d = water_d_0 or water_d_1
water_u = water_u_0 or water_u_1
water_r = water_r_0 or water_r_1
water_l = water_l_0 or water_l_1
water = water_d or water_u or water_r or water_l
water_cover = water_cover_deep or water_cover_shallow
. = grass
- = grass_fold
, = cave_floor
' = dirt
v = rock_ledge
# = stone_wall
\ = gate
* = rock
t = tree
$ = diamond
; = tree_wall

=======
SOUNDS
=======

(rock move)
sfx6 36772507
(rock clatter 11043302)
(player action 75964507)

(get gloves)
sfx0 12173103


(tree cut)
sfx1 75964507

(sheep die)
sfx2 98795903

(candle on/off)
sfx3 8177908

sfx4 87047508

(get axe)
sfx5 80552908

(spooooky echo 24371308)

(spooky shudder 92370508)

(aah.. 24200708)

(baaaar 96148508)
(slow whooosh 61744508)
(shhheeew 83395308)
(badup 17639903)

================
COLLISIONLAYERS
================

background
cave_floor, dirt, grass_fold
tree_ground, tree_edge
dirt_edge
tree_stump, ledge
water
water_edge_r
water_edge_l
water_edge_u
water_edge_d
water_shallows
ground_item
obstacle
sheep_f_np_any
slash_x
hair
gloves
axe
candle_unlit
candle_lit
water_cover
particle

rock_wall_u
rock_wall_l
rock_wall_r
tree_wall_u
tree_wall_l
tree_wall_r
tree_u

dark
call_d
call_u
call_r
call_l

anim_done

======
RULES
======

(ddddarknessssss)
(this messes up editing)

[ cave_floor ] -> [ cave_floor dark_x ]

(walls)
(this messes up editing)

down [ cave_wall | no wall ] -> [ cave_wall_f | ]

down [ rock_wall | no rock_wall_any ] -> [ rock_wall_f | ]
up [ rock_wall_any | no rock_wall ] -> [ rock_wall_any | rock_wall_u ]
right [ no rock_wall_any no ledge no tree_wall_any | rock_wall_any ] -> [ rock_wall_l | rock_wall_any ]
right [ rock_wall_any | no rock_wall_any no ledge no tree_wall_any ] -> [ rock_wall_any | rock_wall_r ]

down [ tree_wall_any | no tree_wall_any ] -> [ tree_wall_f | ]
up [ tree_wall_any | ] -> [ tree_wall_any | tree_wall_u ]
right [ no tree_wall_any | tree_wall_any ] -> [ tree_wall_l | tree_wall_any ]
right [ tree_wall_any | no tree_wall_any ] -> [ tree_wall_any| tree_wall_r ]

right [ water | no water ] -> [ water | water_edge_r ]
left [ water | no water ] -> [ water | water_edge_l ]
up [ water | no water ] -> [ water | water_edge_u ]
down [ water | no water ] -> [ water | water_edge_d ]

(water anim)
[ water_d_1 no anim_done ] -> [ water_d_0 anim_done ]
[ water_d_0 no anim_done ] -> [ water_d_1 anim_done ]
[ water_u_1 no anim_done ] -> [ water_u_0 anim_done ]
[ water_u_0 no anim_done ] -> [ water_u_1 anim_done ]
[ water_r_1 no anim_done ] -> [ water_r_0 anim_done ]
[ water_r_0 no anim_done ] -> [ water_r_1 anim_done ]
[ water_l_1 no anim_done ] -> [ water_l_0 anim_done ]
[ water_l_0 no anim_done ] -> [ water_l_1 anim_done ]
[ anim_done ] -> [ ]

late [ creature water no water_shallows ] -> [ creature water water_cover_deep ]
late [ creature water water_shallows ] -> [ creature water water_shallows water_cover_shallow ]
late [ water_cover no creature ] -> [ ]

(tree top)
late up [ tree | ] -> [ tree | tree_u ]
late up [ no tree | tree_u ] -> [ | ]

(ground transitions)
[ dirt | no wall no dirt no cave_floor ] -> [ dirt | dirt_edge ]
[ tree_wall ] -> [ tree_wall tree_ground ]
[ tree_wall | no tree_ground ] -> [ tree_wall | tree_ground ]
[ tree_ground | no tree_ground ] -> [ tree_ground | tree_edge ]

(tree growth)
[ tree_stump_5 no obstacle ] -> [ tree ]
[ tree_stump_4 ] -> [ tree_stump_5 ]
[ tree_stump_3 ] -> [ tree_stump_4 ]
[ tree_stump_2 ] -> [ tree_stump_3 ]
[ tree_stump_1 ] -> [ tree_stump_2 ]
[ tree_stump_0 ] -> [ tree_stump_1 ]

([ slash_x sheep ] -> [ dead_sheep ])
[ slash_x tree ] -> [ tree_stump_0 ]

([ > rock | rock ] -> [ > rock | > rock ])
[ > player gloves | rock | obstacle ] -> [ rock | player gloves | obstacle ] sfx6
[ > player gloves | rock ] -> [ > player gloves | > rock ] sfx6

[ > player axe | tree ] -> [ player axe | tree slash_x ] sfx1 again
([ > player axe | sheep ] -> [ player axe | sheep slash_x ] sfx2 again)

(sheep start follow (before release so it works))
right [ player | sheep no sheep_follow ] -> [ player | sheep_f_l ]
left [ player | sheep no sheep_follow ] -> [ player | sheep_f_r ]
up [ player | sheep no sheep_follow ] -> [ player | sheep_f_d ]
down [ player | sheep no sheep_follow ] -> [ player | sheep_f_u ]

(call / release sheep action)

[ call_particle_2 ] -> [ ]
[ call_particle_1 ] -> [ call_particle_2 ]
[ call_particle_0 ] -> [ call_particle_1 ]

[ call ] -> []
(
up [ action player | no obstacle ] -> [ action player | call_d ]
down [ action player | no obstacle ] -> [ action player | call_u ]
right [ action player | no obstacle ] -> [ action player | call_l ]
left [ action player | no obstacle ] -> [ action player | call_r ]
)
[ action player no call ] -> [ action player call_d call_u call_r call_l ]

up [ call_d no sheep | no call_d no call_blocker ] -> [ call_d | call_d ]
down [ call_u no sheep | no call_u no call_blocker ] -> [ call_u | call_u ]
right [ call_l no sheep | no call_l no call_blocker ] -> [ call_l | call_l ]
left [ call_r no sheep | no call_r no call_blocker ] -> [ call_r | call_r ]

up [ call_u sheep no sheep_follow no sheep_u ] -> [ call_particle_0 sheep_u ]
down [ call_d sheep no sheep_follow no sheep_d ] -> [ call_particle_0 sheep_d ]
right [ call_r sheep no sheep_follow no sheep_r ] -> [ call_particle_0 sheep_r ]
left [ call_l sheep no sheep_follow no sheep_l ] -> [ call_particle_0 sheep_l ]

(do it again late - sheep walks onto call)
late up [ call_u sheep no sheep_follow no sheep_u ] -> [ call_particle_0 sheep_u ]
late down [ call_d sheep no sheep_follow no sheep_d ] -> [ call_particle_0 sheep_d ]
late right [ call_r sheep no sheep_follow no sheep_r ] -> [ call_particle_0 sheep_r ]
late left [ call_l sheep no sheep_follow no sheep_l ] -> [ call_particle_0 sheep_l ]

(if sheep walks onto call)
(late [ call sheep ] -> [ sheep ])

right [ action player | sheep_follow ] -> [ action player | sheep_r ]
left [ action player | sheep_follow ] -> [ action player | sheep_l ]
up [ action player | sheep_follow ] -> [ action player | sheep_u ]
down [ action player | sheep_follow ] -> [ action player | sheep_d ]

(candle action - after call because they both go)
[ > player no cave_floor | cave_floor ] -> [ > player candle_lit | cave_floor ]
[ > player candle_lit cave_floor | no cave_floor ] -> [ > player candle_unlit cave_floor | ]
[ action player cave_floor candle_lit ] -> [ player cave_floor candle_unlit ] sfx4
[ action player cave_floor candle_unlit ] -> [ player cave_floor candle_lit ] sfx3

(sheep in fold)
up [ sheep grass_fold | stone_wall ] -> [ sheep_in_fold grass_fold | stone_wall ] checkpoint
up [ sheep grass_fold | sheep_in_fold ] -> [ sheep_in_fold grass_fold | sheep_in_fold ] checkpoint

(sheep wander)
[ stationary sheep_r no slash_x ] -> [ right sheep_r ]
[ stationary sheep_l no slash_x ] -> [ left sheep_l ]
[ stationary sheep_u no slash_x ] -> [ up sheep_u ]
[ stationary sheep_d no slash_x ] -> [ down sheep_d ]
right [ > sheep_wander no slash_x | obstacle ] -> [ down sheep_d | obstacle ]
left [ > sheep_wander no slash_x | obstacle ] -> [ up sheep_u | obstacle ]
up [ > sheep_wander no slash_x | obstacle ] -> [ right sheep_r | obstacle ]
up [ > sheep_wander no slash_x | ledge ] -> [ right sheep_r | ledge ]
down [ > sheep_wander no slash_x | obstacle no ledge ] -> [ left sheep_l | obstacle ]

(sheep follow)
[ sheep_follow | player ] -> [ > sheep_follow | player ]

right [ > sheep_follow no slash_x ] -> [ > sheep_f_r ]
left [ > sheep_follow no slash_x ] -> [ > sheep_f_l ]
up [ > sheep_follow no slash_x ] -> [ > sheep_f_u ]
down [ > sheep_follow no slash_x ] -> [ > sheep_f_d ]

late right [ sheep_follow | no player ] -> [ sheep_follow sheep_f_np_0 | ]
late left [ sheep_follow sheep_f_np_0 | no player ] -> [ sheep_follow sheep_f_np_1 | ]
late up [ sheep_follow sheep_f_np_1 | no player ] -> [ sheep_follow sheep_f_np_2 | ]
late down [ sheep_follow sheep_f_np_2 | no player ] -> [ sheep_follow sheep_f_np | ]
late [ sheep_f_r sheep_f_np ] -> [ sheep_r ]
late [ sheep_f_l sheep_f_np ] -> [ sheep_l ]
late [ sheep_f_u sheep_f_np ] -> [ sheep_u ]
late [ sheep_f_d sheep_f_np ] -> [ sheep_d ]
late [ sheep_f_np_any ] -> []

(ledge)
down [ creature ledge | no obstacle ] -> [ > creature ledge | ]

(cancel sheep_follow on stationary player (for water))
[ > sheep_follow | stationary player ] -> [ sheep_follow | player ]

(water)
down [ stationary creature water_d_0 water_shallows ] -> [ > creature water_d_0 water_shallows ]
down [ creature water_d no water_shallows ] -> [ > creature water_d ]
up [ stationary creature water_u_0 water_shallows ] -> [ > creature water_u_0 water_shallows ]
up [ creature water_u no water_shallows ] -> [ > creature water_u ]
right [ stationary creature water_r_0 water_shallows ] -> [ > creature water_r_0 water_shallows ]
right [ creature water_r no water_shallows ] -> [ > creature water_r ]
left [ stationary creature water_l_0 water_shallows ] -> [ > creature water_l_0 water_shallows ]
left [ creature water_l no water_shallows ] -> [ > creature water_l ]

(item pickup)
late [ player gloves_item ] -> [ player gloves ] sfx0
late [ player axe_item ] -> [ player axe ] sfx5
late [ player candle_item ] -> [ player candle_lit ] sfx3
late [ player hair_item ] -> [ player hair_held ]

(ensure held items stay held)
late [ player | ... | held_item ] -> [ player held_item | ... | ]

late right [ | player hair_held | ] -> [ hair_l | player hair_held | hair_r ]
late up [ player hair_held | ] -> [ player hair_held | hair_u ]
late right [ hair_l | no hair_held ] -> [ | ]
late right [ no hair_held | hair_r ] -> [ | ]
late up [ no hair_held | hair_u ] -> [ | ]

(let there be...candles!)
late [ light dark ] -> [ light dark_0 ]
late [ dark_0 | dark_x ] -> [ dark_0 | dark_1 ]
late [ dark_1 | dark_x ] -> [ dark_1 | dark_2 ]
late [ dark_2 | dark_x ] -> [ dark_2 | dark_3 ]
late [ dark_3 | dark_x ] -> [ dark_3 | dark_4 ]

==============
WINCONDITIONS
==============

(all grass_fold on sheep_in_fold)

=======
LEVELS
=======

..;;;;||||||||||
.;;;;;.&...''''|
;;;;;.....'''''|
;;;;;;...''''''|
;;;;;;..'''''''|
;;........'''|||
;.......||v|||||
;......*''''1''|
;...p.g.'''''''|
;;.....*.''''''|
;........'|'''||
;;.....$..||||||
;;;.......||||||
;;;;;.;..;||||||
;;;;;;;;;;||||||
;;;;;;;;;;||||||

]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]||||||||||||||||||||||||||||||||
]]]]]]]]]]]]]]]]],,,],]],,,,,,]]|||||||||||||||||'''''|'''||||||
]]]]]]]]]]]]]]]]],],,,]],,]],,,]||||||t||||||||||''||v|'..*..|||
]]]]]]]]]]]]]],,,,],,,,]],]],,,]||||||&.''''''|||''||'..g.*..;||
]]]]],,,,,,],]3]],,,]],,,,]],]]]||||||.||||||||||'''|||...|*|;||
]]]],,,,,,,,,,,]]]]]]]]]],,,,,,]||||||.|||||||||||'''||||||.||||
]]]],,,,,,,,,,,,,,,,,,,]],],]],]||||||.|;;;;;;;||||'''|||||v|||;
]]]]],,,,,,]],,]]],]]],]],]]]],,'''|||.|;;.;.;;||'||'''''|''.;.;
]]]]]]]],,]]]]]]]],,]],],,,,]],]|''||t........'''''|''''2''|'..;
]]]]]]]],,,,]]]]]]],,],],,],,,,]||2||||...|.|''|||v|'''''''|'.&|
]]]0,,,],,,,,]]]]]],,,,],]],,]]]||'''||||...|''|||'''''''|'|'..|
]],,,,,,,,,,,]]]]]]],,,]]]]],],]|''''''||||||''|||''||||||'''|||
]]],,,,,],,,,]]]]]]]],,],],,,,,]|''''''''||''''||'''2'''||||||||
]]]]],]]]]]]]]]]]]]]]],],,,,],]]||''''''''''|''||'''|''''|||||||
]]]]]]]]]]]]]]]]]]]]]],]],],],,]|||'''''''''|||||||||''''|||||||
]]]]]]]]]]]]]]]]]]]]]],]]]]]]]]]|||||||||'||||||||||||||'|||||||
||||||||||||||||||||||'||||||||||||||||||'||||||||||||||'|||||||
||||||||||||||'|||||||v|||'''|||||||||||'''|''||||||||||v|||||||
||'''''''''''''||||||''''''''||||||||''|'''|''||||||||'''''|||||
||v|||||||||||'|||||'''''|2'''|||||...''''''''||||||||'''''|||||
;''|'|||||||||'||||||''|''''''|||;;;;..|||;;.'||||||'||''''||||;
;1'|'''''''''|'|||||||||''''''|||;;;.&.....;.''|||||2|||||2||||;
;''|''''||'|||'||||||||||v|||||||;;;..;;......'||||''''''''||||;
;''2'''''|'|'|2||||||||||''||||||;;;..;;.....''||'||'|||||'||||;
;|||||''||v|''';;||'''''''''|||||.....;;...*..'''''|'''''|||||;;
;''||||||'''...;;;''.......';;;||.;...;;;.....'||''''''''''2'|;;
;;''''''''.............;....;.....;;..........;||||'''''..||'|;;
;;.............;;.............;;;.;;.......;;;;;||''.........;;;
;;.............;;.............;;;;;;;;....;;;;;;||'....;;.;;;;;;
;;;..;;....;..;;;.............;;;;;;;...;;;;;;;;;;....;.;.;;;;;;
;;;;.;;;.;;;..;;;;.......;...;;;;;;;;;;;;;;;;;;;;;;...;...;;;;;;
;;;;;;;;.;;;;;;;;;;;;;.;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;.;;;;;;;;
;;;;;;;;.;;;;;;;;;;;;;.;;;;;;;;;.............;;;;;;;;;;.;;;;;;;;
;;;;;;;;......;;;;;;;.....;;;.;;.############..;;;;;;....;;;;;;;
;;;..;;;.....;;;;;;;;;;....###...#.........;###.#;.;.....;;;;;;;
;;...;;;......;;;;...&.......##..#............#.#;.......;;;;;;;
;...&.;;.....;;;;;.;;.........##.#............###........;;;;;;;
;.....;.......;;;;.;;..........#.#......;..................;;;;;
;.;...*..;...;.;;;.*.....;.;.;.###..######....###.;....;;.;;;;;;
;.....;.;;....;;;...................#----#.;..#.#...&...;...;;;;
;.....;;;......;;;.;.....;.;.;.###..#----#..p.#.#............;;;
;;...;;........;;;.;;..........#.#.;#----.....#.#..........;;;;;
;;;;;;''''.....;;;..;.........##.#..######\...#.#;......;...;;;|
;;;;|||v||t|'.;;;;...........##..#.........&..#.#;;..;.;;''.'.||
;||||''''..||;;;;;...........#...#............#.#||''''''''''|||
;;;''.......;;;;;;;.....;...##...#........;...#.#||'|'|||'''||||
;;;;......;;;;;;;;;;;.......;....##############'|||||'''''||||||
;;;;;;;.;;;;;;;;;;;;;;;;.;;;;...............'''|||||||||'|||||||
;;;;;;;.;;;;;;;;;;;;;;;;.;;;;;....;;;;||||||||||]]]]]]]]']]]]]]]
;;;;;;;...;;;...t...;;;...;;;;;..;;;;;.&...''''|]]],],,],,]],]]]
;;;;;;;;..;;|'';;;..;.;.......;;;;;;;.....'''''|]],,],,,,,,],,]]
;;;;;....;..|v|;;.....t.......;;;;;;;;...''''''|]],,],]],,,,,,]]
;;;......t...'||;.....;;.......;;;;;;;;.'''''''|]],,,,,,,,],,]]]
;;....a.;....;;|;.....;;..;....;;;..;|||''''''||]]],,,],,,]],,,]
;;......;.....;|;.....;;.....;;;;....||||''''''|]]]],]],c,,]],,]
;;;..;...;|v||||;...;;;;......;;;....'''|'''''''',]],,],,,,,,,]]
;;;t;;;t;||''.t......;;;.;;;...........'|''''''|],,]],]]]]]],,]]
;;;.&...;|;'..;;;....;;;t;;;;..;;;.....'|''''''|]],]],],,],]],,]
;;....;.;;;...;;;...;;....;;;;;;;.......|v|'''||]],]],,,,,,,,,,]
;;......;;;;.;;;;;.;;;...;.&..;;;;.......'||||||]],,]]]]],]]],]]
;;;.....;;;;;;;;;;;;;;;.....;.;;;;;.......||||||]],,,,]]],],,,,]
;;;...;;;;;;;;;;;;;;;;;;t...;..;;;;;;.;..;||||||]]]]],]],,]]],]]
;;;;;;;;;;;;;;;;;;;;;;;;;.tt..;;;;;;;;;;;;||||||]]],,,,,,,,,]]]]
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;||||||]]]]]]]]]]]]]]]]