title Sheepfold
author Nuno Das Neves
homepage www.puzzlescript.net
text_color #ffaabb
background_color darkgreen

(this messes up editing)
(run_rules_on_level_start)
realtime_interval 0.5
again_interval 0.1
(key_repeat_interval 0.2)
throttle_movement

(zoomscreen 8x8)
flickscreen 16x16

debug

========
OBJECTS
========

grass .
#60b020 #509020
11111
00111
11100
11111
11001

dirt_grass
#664411 #765411 #60b020 #509020
00333
20110
11330
00022
33011

grass_fold -
#60b020 #509020
01111
00110
11100
10111
11001

cave_floor ,
#373025 #453721
00000
00110
11000
00000
00011

cave_wall
#191714

cave_wall_f
#302519 #191714
11111
11111
11000
01010
00010

dirt '
#664411 #765411
00000
00110
11000
00000
00011

rock_wall |
#352721 darkbrown
00100
10000
00000
01000
00001

rock_wall_f
darkbrown #352721
11111
00101
00110
01010
00001

rock_ledge v
darkbrown #765411 brown #664411 #352721
23332
02221
44040
44044
34401

cave_ledge
darkbrown #302519 #664411 #352721  #191714  
23332
02221
44040
44044
34401

dark_0
transparent

dark_1
transparent

dark_2
black
.....
.0.0.
.....
.0...
...0.

dark_3
black
.00.0
...0.
.00..
...00
.0...

dark_4
black
000.0
0.000
0000.
00.00
0.000

dark_x
black

player p
black brown #eee blue
.000.
.010.
13331
13331
.333.

slash_x
white

sheep_r &
#ddd #aaa #fed060
...00
00012
0000.
2112.
2..2.

sheep_l
#ddd #aaa #fed060
...00
00012
0000.
2112.
2..2.

sheep_d
#ddd #aaa #fed060
...00
00012
0000.
2112.
2..2.

sheep_u
#ddd #aaa #fed060
...00
00012
0000.
2112.
2..2.

sheep_in_fold
#ddd #aaa #fed060
.....
...00
00012
0000.
22022
(
dead_sheep
#ddd #aaa #fed060 red
.....
2..2.
2113.
00302
03301
)

gloves
darkgreen #aa2299
.....
.....
1...1
0...0
.....

gloves_item
darkgreen #aa2299
...00
..000
00.11
000..
11...

axe
black orange
.....
.....
.....
0111.
00...

axe_item
orange black
.0.1.
.1111
.0111
.0.1.
.0...

candle_item
#ff6600 #eed0a0 white #ffee00 gray
..0..
.030.
.020.
..4..
..4..

candle_unlit
#ff6600 #eed0a0 white yellow
.....
.....
...1.
...1.
.....

candle_lit
#ff6600 #eed0a0 white yellow
.....
...0.
..323
...1.
.....

stone_wall #
darkgray darkbrown gray
22212
10110
01001
10010
00010

gate \
brown darkbrown
00000
01..0
0.1.0
0..10
11111

rock *
gray darkgray
.000.
00000
00000
10001
11111

tree t
brown #408030 #306020
.111.
21221
20121
.020.
..0..

tree_stump_0
brown
.....
.....
.....
.....
.....

tree_stump_1
darkbrown
.....
.....
.....
..0..
.....

tree_stump_2
darkbrown
.....
.....
.....
..0..
.....

tree_stump_3
brown
.....
.....
.....
..0..
.....

tree_stump_4
brown
.....
.....
.....
..0..
.....

tree_stump_5
brown #408030
.....
.....
.0.1.
..0..
.....

tree_wall ;
darkbrown #306020 #204010
.211.
11121
21112
12212
.122.

tree_wall_f
darkbrown #306020 #204010
.111.
12111
20212
20120
.220.

diamond $
orange white brown
.110.
11000
00002
.002.
..2..

=======
LEGEND
=======

background = grass
wall = stone_wall or cave_wall or cave_wall_f or tree_wall or tree_wall_f or gate or rock_wall or rock_wall_f or rock_ledge or cave_ledge
ground_item = gloves_item or axe_item or diamond or candle_item
held_item = gloves or axe or candle_unlit or candle_lit
sheep = sheep_r or sheep_l or sheep_d or sheep_u
pushable = rock
obstacle = player or wall or rock or tree or sheep or pushable or sheep_in_fold
light = candle_item or candle_lit
] = cave_floor and cave_wall
dark = dark_0 or dark_1 or dark_2 or dark_3 or dark_4 or dark_x
ledge = rock_ledge or cave_ledge
tree_stump = tree_stump_0 or tree_stump_1 or tree_stump_2 or tree_stump_3 or tree_stump_4 or tree_stump_5
g = gloves_item
a = axe_item
c = cave_floor and candle_item
0 = sheep_r and cave_floor
1 = sheep_r and dirt
2 = rock and dirt
3 = cave_ledge and cave_floor

=======
SOUNDS
=======

(rock move)
sfx6 36772507
(rock clatter 11043302)
(player action 75964507)

(get gloves)
sfx0 12173103


(tree cut)
sfx1 75964507

(sheep die)
sfx2 98795903

(candle on/off)
sfx3 8177908

sfx4 87047508

(get axe)
sfx5 80552908

(spooooky echo 24371308)

(spooky shudder 92370508)

(aah.. 24200708)

(baaaar 96148508)
(slow whooosh 61744508)
(shhheeew 83395308)
(badup 17639903)

================
COLLISIONLAYERS
================

background
cave_floor, dirt, grass_fold, dirt_grass
tree_stump
ground_item
obstacle
slash_x
gloves
axe
candle_unlit
candle_lit
dark

======
RULES
======

(ddddarknessssss)
(this messes up editing)

[ cave_floor ] -> [ cave_floor dark_x ]

(walls)
(this messes up editing)

down [ cave_wall | no wall ] -> [ cave_wall_f | ]
down [ tree_wall | no wall ] -> [ tree_wall_f | ]
down [ rock_wall | no wall ] -> [ rock_wall_f | ]

(dirt grass transition)
[ dirt | grass no wall no dirt no cave_floor ] -> [ dirt | dirt_grass ]

(tree growth)
[ tree_stump_5 no obstacle ] -> [ tree ]
[ tree_stump_4 ] -> [ tree_stump_5 ]
[ tree_stump_3 ] -> [ tree_stump_4 ]
[ tree_stump_2 ] -> [ tree_stump_3 ]
[ tree_stump_1 ] -> [ tree_stump_2 ]
[ tree_stump_0 ] -> [ tree_stump_1 ]

([ slash_x sheep ] -> [ dead_sheep ])
[ slash_x tree ] -> [ tree_stump_0 ]

([ > rock | rock ] -> [ > rock | > rock ])
[ > player gloves | rock | obstacle ] -> [ rock | player gloves | obstacle ] sfx6
[ > player gloves | rock ] -> [ > player gloves | > rock ] sfx6

[ > player axe | tree ] -> [ player axe | tree slash_x ] sfx1 again
([ > player axe | sheep ] -> [ player axe | sheep slash_x ] sfx2 again)

[ action player candle_lit ] -> [ player candle_unlit ] sfx4
[ action player candle_unlit ] -> [ player candle_lit ] sfx3

(sheep)
up [ sheep grass_fold | stone_wall ] -> [ sheep_in_fold grass_fold | stone_wall ] checkpoint
up [ sheep grass_fold | sheep_in_fold ] -> [ sheep_in_fold grass_fold | sheep_in_fold ] checkpoint

[ stationary sheep_r no slash_x ] -> [ right sheep_r ]
[ stationary sheep_l no slash_x ] -> [ left sheep_l ]
[ stationary sheep_u no slash_x ] -> [ up sheep_u ]
[ stationary sheep_d no slash_x ] -> [ down sheep_d ]
right [ > sheep no slash_x | obstacle ] -> [ down sheep_d | obstacle ]
left [ > sheep no slash_x | obstacle ] -> [ up sheep_u | obstacle ]
up [ > sheep no slash_x | obstacle ] -> [ right sheep_r | obstacle ]
down [ > sheep no slash_x | obstacle no ledge ] -> [ left sheep_l | obstacle ]

(ledge)
down [ > player | ledge | no obstacle ] -> [ | ledge | player ]
down [ > sheep | ledge | no obstacle ] -> [ | ledge | sheep ]

(item pickup)
late [ player gloves_item ] -> [ player gloves ] sfx0
late [ player axe_item ] -> [ player axe ] sfx5
late [ player candle_item ] -> [ player candle_lit ] sfx3

(hold onto held items)
late [ player | ... | held_item ] -> [ player held_item | ... | ]

(let there be...candles!)
late [ light dark ] -> [ light dark_0 ]
late [ dark_0 | dark_x ] -> [ dark_0 | dark_1 ]
late [ dark_1 | dark_x ] -> [ dark_1 | dark_2 ]
late [ dark_2 | dark_x ] -> [ dark_2 | dark_3 ]
late [ dark_3 | dark_x ] -> [ dark_3 | dark_4 ]

==============
WINCONDITIONS
==============

all grass_fold on sheep_in_fold

=======
LEVELS
=======

]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]||||||||||||||||||||||||||||||||
]]]]]]]]]]]]]]]]],,,],]],,,,,,]]|||||||||||||||||'''''|'''||||||
]]]]]]]]]]]]]]]]],],,,]],,]],,,]||||||t||||||||||''||v|'..*..|||
]]]]]]]]]]]]]],,,,],,,,]],]],,,]||||||&.''''''|||''||'..g.*..;||
]]]]],,,,,,],]3]],,,]],,,,]],]]]||||||.||||||||||'''|||...|*|;||
]]]],,,,,,,,,,,]]]]]]]]]],,,,,,]||||||.|||||||||||'''||||||.||||
]]]],,,,,,,,,,,,,,,,,,,]],],]],]||||||.|;;;;;;;||||'''|||||v|||;
]]]]],,,,,,]],,]]],]]],]],]]]],,'''|||.|;;.;.;;||'||'''''|''.;.;
]]]]]]]],,]]]]]]]],,]],],,,,]],]|''||t........'''''|''''2''|'..;
]]]]]]]],,,,]]]]]]],,],],,],,,,]||2||||...|.|''|||v|'''''''|'.&|
]]]0,,,],,,,,]]]]]],,,,],]],,]]]||'''||||...|''|||'''''''|'|'..|
]],,,,,,,,,,,]]]]]]],,,]]]]],],]|''''''||||||''|||''||||||'''|||
]]],,,,,],,,,]]]]]]]],,],],,,,,]|''''''''||''''||'''2'''||||||||
]]]]],]]]]]]]]]]]]]]]],],,,,],]]||''''''''''|''||'''|''''|||||||
]]]]]]]]]]]]]]]]]]]]]],]],],],,]|||'''''''''|||||||||''''|||||||
]]]]]]]]]]]]]]]]]]]]]],]]]]]]]]]|||||||||'||||||||||||||'|||||||
||||||||||||||||||||||'||||||||||||||||||'||||||||||||||'|||||||
||||||||||||||'|||||||v|||'''|||||||||||'''|''||||||||||v|||||||
||'''''''''''''||||||''''''''||||||||''|'''|''||||||||'''''|||||
||v|||||||||||'|||||'''''|2'''|||||...''''''''||||||||'''''|||||
;''|'|||||||||'||||||''|''''''|||;;;;..|||;;.'||||||'||''''||||;
;1'|'''''''''|'|||||||||''''''|||;;;.&.....;.''|||||2|||||2||||;
;''|''''||'|||'||||||||||v|||||||;;;..;;......'||||''''''''||||;
;''2'''''|'|'|2||||||||||''||||||;;;..;;.....''||'||'|||||'||||;
;|||||''||v|''';;||'''''''''|||||.....;;...*..'''''|'''''|||||;;
;''||||||'''...;;;''.......';;;||.;...;;;.....'||''''''''''2'|;;
;;''''''''.............;....;.....;;..........;||||'''''..||'|;;
;;.............;;.............;;;.;;.......;;;;;||''.........;;;
;;.............;;.............;;;;;;;;....;;;;;;||'....;;.;;;;;;
;;;..;;....;..;;;.............;;;;;;;...;;;;;;;;;;....;.;.;;;;;;
;;;;.;;;.;;;..;;;;.......;...;;;;;;;;;;;;;;;;;;;;;;...;...;;;;;;
;;;;;;;;.;;;;;;;;;;;;;.;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;.;;;;;;;;
;;;;;;;;.;;;;;;;;;;;;;.;;;;;;;;;.............;;;;;;;;;;.;;;;;;;;
;;;;;;;;......;;;;;;;.....;;;.;;.############..;;;;;;....;;;;;;;
;;;..;;;.....;;;;;;;;;;....###...#.........;###.#;.;.....;;;;;;;
;;...;;;......;;;;...&.......##..#............#.#;.......;;;;;;;
;...&.;;.....;;;;;.;;.........##.#............###........;;;;;;;
;.....;.......;;;;.;;..........#.#......;..................;;;;;
;.;...*..;...;.;;;.*.....;.;.;.###..######....###.;....;;.;;;;;;
;.....;.;;....;;;...................#----#.;..#.#...&...;...;;;;
;.....;;;......;;;.;.....;.;.;.###..#----#..p.#.#............;;;
;;...;;........;;;.;;..........#.#.;#----.....#.#..........;;;;;
;;;;;;''''.....;;;..;.........##.#..######\...#.#;......;...;;;|
;;;;|||v||t|'.;;;;...........##..#.........&..#.#;;..;.;;''.'.||
;||||''''..||;;;;;...........#...#............#.#||''''''''''|||
;;;''.......;;;;;;;.....;...##...#........;...#.#||'|'|||'''||||
;;;;......;;;;;;;;;;;.......;....##############'|||||'''''||||||
;;;;;;;.;;;;;;;;;;;;;;;;.;;;;...............'''|||||||||'|||||||
;;;;;;;.;;;;;;;;;;;;;;;;.;;;;;....;;;;||||||||||]]]]]]]]']]]]]]]
;;;;;;;...;;;...t...;;;...;;;;;..;;;;;.&...''''|]]],],,],,]],]]]
;;;;;;;;..;;|'';;;..;.;.......;;;;;;;.....'''''|]],,],,,,,,],,]]
;;;;;....;..|v|;;.....t.......;;;;;;;;...''''''|]],,],]],,,,,,]]
;;;......t...'||;.....;;.......;;;;;;;;.'''''''|]],,,,,,,,],,]]]
;;....a.;....;;|;.....;;..;....;;;..;|||''''''||]]],,,],,,]],,,]
;;......;.....;|;.....;;.....;;;;....||||''''''|]]]],]],c,,]],,]
;;;..;...;|v||||;...;;;;......;;;....'''|'''''''',]],,],,,,,,,]]
;;;t;;;t;||''.t......;;;.;;;...........'|''''''|],,]],]]]]]],,]]
;;;.&...;|;'..;;;....;;;t;;;;..;;;.....'|''''''|]],]],],,],]],,]
;;....;.;;;...;;;...;;....;;;;;;;.......|v|'''||]],]],,,,,,,,,,]
;;......;;;;.;;;;;.;;;...;.&..;;;;.......'||||||]],,]]]]],]]],]]
;;;.....;;;;;;;;;;;;;;;.....;.;;;;;.......||||||]],,,,]]],],,,,]
;;;...;;;;;;;;;;;;;;;;;;t...;..;;;;;;.;..;||||||]]]]],]],,]]],]]
;;;;;;;;;;;;;;;;;;;;;;;;;.tt..;;;;;;;;;;;;||||||]]],,,,,,,,,]]]]
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;||||||]]]]]]]]]]]]]]]]